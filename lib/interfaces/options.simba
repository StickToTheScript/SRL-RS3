(*
Options
=======

The options file holds functions and procedures that are used to interact with the runescape options menu.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/options.simba>`_.

*)

{$f-}

(*
Consts, Types, Vars
~~~~~~~~~~~~~~~~~~~

The following constants, types and variables are used throughout the collectBox methods.

**type TRSOptions**

.. code-block:: pascal

    type
      TRSOptions = type TRSInterface;

A type that stores functions and properties of the Runescape options menu.
*)
type
  TRSOptions = type TRSInterface;

(*
**var options**

.. code-block:: pascal

    var
      options: TRSOptions;

A variable through which scripters can access the TRSOptions type methods.

*)
var
  options: TRSOptions;

(*
TRSOptions methods
~~~~~~~~~~~~~~~~~~

The following methods should be called through the **options** variable.

Example:

.. code-block:: pascal

    if options.isOpen() then
      writeln('Options interface is open!');
*)

{*
__init
------

.. code-block:: pascal

    procedure TRSOptions.__init();

Initializes the TRSOptions.

.. note::

    - by Coh3n
    - Last updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    options.__init();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSOptions.__init();
begin
  with self do
  begin
    name := 'RS Options Menu';
    ID := ID_INTERFACE_OPTIONS;
    parentID := -1;
    isStatic := false;
    setBounds(IntToBox(277, 267, 746, 500));
  end;
end;
{$ENDIF}

{*
__find
------

.. code-block:: pascal

    function TRSOptions.__find(): boolean;

Returns true if the options interface is detected and its bounds are set.
Scripters should use options.isOpen() below.

.. note::

    - by Coh3n
    - Last Updated: 18 June 2020 by 3Garrett3

Example:

.. code-block:: pascal

    if options.__find() then
      writeln('The options interface is open, and the bounds are set!');
*}
{$IFNDEF CODEINSIGHT}
function TRSOptions.__find(): Boolean;
var
  i: Integer;
  TPA: TPointArray;
  ATPA: T2DPointArray;
  b, tb: TBox;
begin
  findColorsTolerance(TPA, 2045534, getClientBounds(), 7, colorSetting(2, 0.14, 0.81));

  if length(TPA) < 1000 then
  begin
    print('TRSOptions.__find(): Didn''t find brown bar', TDebug.WARNING);
    exit(false);
  end;

  ATPA := TPA.cluster(3);
  ATPA.sortBySize(true);

  //print('Length of ATPA: ' + toStr(length(ATPA)));
  for i := 0 to high(ATPA) do
  begin
    b := ATPA[i].getBounds();

    if inRange(b.getWidth(), 425, 500) and inRange(b.getHeight(), 20, 30) then
    begin
      tb := b;
      tb.edit(round(b.getWidth()*0.3), 0, -round(b.getWidth()*0.3), 0);

      if (pos('OPTIONS', upperCase(TesseractOCR.Recognize(tb, 50))) > 0) then
      begin
        self.setBounds([b.x1, b.y1 - 3, b.x2, b.y1 + 226]);
        exit(true);
      end;
    end;
    print('tRSOptions.__find(): Failed - Width: ' + toStr(b.getWidth()) + ', Height: ' + toStr(b.getHeight()));
  end;

  print('TRSOptions.__find(): Could not find the options interface', TDebug.WARNING);
end;
{$ENDIF}

(*
isOpen
------

.. code-block:: pascal

    function TRSOptions.isOpen(waitTime: integer = 0): boolean;

Returns true if the options interface is open. The optional **waitTime** parameter
*(default = 0)* is the maximum time (in milliseconds) it will keep looking.

.. note::

    - by Coh3n
    - Last Updated: 5 August 2015 by The Mayor

Example:

.. code-block:: pascal

    if options.isOpen() then
      writeln('The options menu is open!');
*)
function TRSOptions.isOpen(waitTime: integer = 0): boolean;
var
  t: UInt64;
begin
  t := (GetTickCount() + waitTime);

  repeat
    if self.__find() then
    begin
      result := true;
      break();
    end;

    wait(randomRange(150, 250));
  until (getSystemTime() >= t);

  print('TRSOptions.isOpen() result: ' + boolToStr(result));
end;

(*
open
----

.. code-block:: pascal

    function TRSOptions.open(waitTime: integer = 5000): boolean;

Returns true if the options interface is successfully opened. The optional
**waitTime** parameter *(default = 5000)* is the maximum time (in milliseconds)
it will keep looking.

.. note::

    - by Coh3n
    - Last Updated: 5 August 2015 by The Mayor

Example:

.. code-block:: pascal

    if (options.open()) then
      writeln('We opened the options interface');
*)
function TRSOptions.open(waitTime: integer = 7500): boolean;
var
  t: UInt64;
begin
  if self.isOpen() then
    exit(true);

  typeByte(VK_ESCAPE);

  t := (GetTickCount() + waitTime + random(400));

  while (GetTickCount() < t) do
    if self.isOpen(2000) then
    begin
      result := true;
      break();
    end else
      typeByte(VK_ESCAPE);
end;

(*
_getOptions
-----------

.. code-block:: pascal

    function TRSOptions._getOptions(var Buttons: TBoxArray; var Texts: TStringArray): boolean;

Passes all the buttons on the options menu as a TBoxArray **Buttons** and passes
a TStringArray **Texts** of the text for each button. Returns true if successfull.

.. note::

    - by Ashaman88
    - Last Updated: 26 February 2014 by Ashaman88

Example:

.. code-block:: pascal

    var
      txt: TStringArray;
      tba: TBoxArray;
    begin
      if options._getOptions(tba, txt) then
        writeLn('We have all the button bounds and text');
    end;
*)
function TRSOptions._getOptions(var Buttons: TBoxArray; var Texts: TStringArray): boolean;
var
  TPA: TPointArray;
  ATPA: T2DPointArray;
  i: Integer;
  b: TBox;
begin
  Result := True;
  if (not Self.isOpen()) then
    Exit(False);

  if (not FindColorsTolerance(TPA, 9998984, Self.getBounds(), 10, ColorSetting(1))) then
    Exit(False);

  ATPA := ClusterTPAEx(TPA, 5, 1);

  for i := 0 to High(ATPA) do
  begin
    b := GetTPABounds(ATPA[i]);
    Inc(b.y2, 18);

    if ((b.X2 - b.X1) > 30) then
    begin
      Insert(b, Buttons);
      Insert(TesseractOCR.Recognize(b, 55), Texts);
    end;
  end;
end;

(*
selectOption
------------

.. code-block:: pascal

    function TRSOptions.selectOption(txt: TStringArray): boolean;

Returns true if it successfully clicks the button on the options menu that
matches a string in the TStringArray **txt** .

.. note::

    - by Ashaman88
    - Last Updated: 26 February 2014 by Ashaman88

Example:

.. code-block:: pascal

    if options.selectOption(['Logout', 'gout']) then
      writeLn('Successfully clicked the logout button!');

*)
function TRSOptions.selectOption(txt: TStringArray): boolean;
var
  texts: TStringArray;
  i, j: Integer;
  b: TBoxArray;
begin
  if not self.Open() then
    exit(false);

  self._getOptions(b, texts);

  for i := 0 to high(txt) do
    for j := 0 to high(texts) do
      if (pos(txt[i], texts[j]) > 0) then
      begin
        mouseBox(b[j], MOUSE_LEFT);
        exit(true);
      end;
end;

begin
  options.__init();
end;

{$f+}
